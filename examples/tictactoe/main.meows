~( ^.x.^)> ---------------------------------------------
Meowscript implementation of a simple 'Tic Tac Toe' game
with dynamic board size.

This yarn ball is the 'main'.
--------------------------------------------- <(^.x.^ )~

takes "board.meows"
takes "assert.meows"
takes "exception.meows"

=^.x.^= tictactoe(size)
    assert(size > 1, "Board size cannot be less than 1!")
    mew board = make_board(size)

    mew state = GameState.continue
    mew turn = Turn.x

    meowmeow (state == GameState.continue)
        print_board(board)
        meow("Turn: "..turn_tile(turn))
        mew row = get_input(size, "row")
        mew col = get_input(size, "column")

        mew? (board.get([row, col]) != Tile.empty)
            meow("Position is already filled!")
            catnap
        meow meow
        
        board.add([row, col], turn_tile(turn))
        turn = change_turn(turn)
        state = game_state(board)
    meow meow

    print_board(board)

    mew? (state == GameState.draw)
        meow("Draw.")
    hiss!
        mew winner = state == GameState.x_won ? "x" : "o"
        meow("The winner is "..winner.."!")
    meow meow
meow meow

=^.x.^= get_input(size, name)
    mew bad_input = "Invalid input! Please pick a number between 1 and "..size.."!"
    mew message = "Enter "..name..": "

    meowmeow (happy)
        watch
            purr(message)
            mew input = int(listen()) - 1
            mew? (input >= 0 and input <= size - 1)
                bring input
            hiss!
                meow(bad_input)
            meow meow
        catch (MeowException.InvalidValue)
            meow(bad_input)
        meow meow
    meow meow
meow meow


-- Run main:
mew args = snoop()
mew board_size = 3

mew? (not args)
    meow("Expected board size, got no arguments!")
    meow("Defaulting board size to: "..board_size)
hiss!
    watch
        board_size = int(peek args)
    catch (MeowException.InvalidValue) 
        meow("Invalid board size: "..(peek args))
        board_size = 3
        meow("Defaulting board size to: "..board_size)
    meow meow
meow meow

tictactoe(board_size)
